apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "maven"
apply plugin: "org.jetbrains.dokka"
apply plugin: "com.jfrog.bintray"
apply plugin: "jacoco"
apply plugin: "com.github.ben-manes.versions"

def libraryName = "mmupnp"

group pj.groupId
archivesBaseName = libraryName
version pj.versions.name

test.maxParallelForks = 4

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api "net.mm2d:log:0.8.4"

    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "com.google.truth:truth:1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2"
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

dokka {
    outputFormat = "html"
}

bintray {
    user = project.hasProperty("bintray_user") ? bintray_user : ""
    key = project.hasProperty("bintray_key") ? bintray_key : ""
    configurations = ["archives"]

    pkg {
        repo = "maven"
        name = pj.groupId + "." + libraryName
        licenses = ["MIT"]
        websiteUrl = pj.siteUrl
        issueTrackerUrl = pj.githubUrl + "/blob/master/LICENSE"
        vcsUrl = pj.githubUrl + ".git"
        issueTrackerUrl = pj.githubUrl + "/issues"
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name = libraryName
            url = pj.siteUrl

            groupId = pj.groupId
            artifactId = libraryName
            version = pj.versions.name

            licenses {
                license {
                    name = "The MIT License"
                    url = "https://opensource.org/licenses/MIT"
                    distribution = "repo"
                }
            }
            scm {
                connection pj.githubUrl + ".git"
                url pj.githubUrl
            }
        }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file:build/maven")
        pom.project {
            name = libraryName
            url = pj.siteUrl

            groupId = pj.groupId
            artifactId = libraryName
            version = pj.versions.name
            licenses {
                license {
                    name = "The MIT License"
                    url = "https://opensource.org/licenses/MIT"
                    distribution = "repo"
                }
            }
            scm {
                connection pj.githubUrl + ".git"
                url pj.githubUrl
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jacoco {
    toolVersion = "0.8.3"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview", "b", "ea"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}
